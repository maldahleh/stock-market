plugins {
    id 'java'
    id 'jacoco'
    id 'io.freefair.lombok' version '6.3.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.0'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://nexus.hc.to/content/repositories/pub_releases' }
    maven { url 'https://repo.citizensnpcs.co/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.phoenix616.dev/' }
}

dependencies {
    library 'com.google.code.gson:gson:2.8.9'
    bukkitLibrary 'com.google.code.gson:gson:2.8.9'

    implementation 'de.sfuhrm:YahooFinanceAPI:3.16.2'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.28-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.mockito:mockito-inline:4.1.0'
    testImplementation 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    testImplementation 'net.milkbowl.vault:VaultAPI:1.7'
    testImplementation 'net.citizensnpcs:citizensapi:2.0.28-SNAPSHOT'
    testImplementation 'me.clip:placeholderapi:2.10.10'
}

group = 'com.maldahleh'
version = '3.0.0'
description = 'StockMarket'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.encoding = 'UTF-8'
}

bukkit {
    name = 'StockMarket'
    description = 'Buy and sell stocks.'
    author = 'maldahleh'
    main = 'com.maldahleh.stockmarket.StockMarket'
    apiVersion = '1.17'

    depend = ['Vault']
    softDepend = ['Citizens', 'PlaceholderAPI']

    commands {
        stockmarket {
            description = 'Use the stock market'
            aliases = ['sm', 'stocks', 'stock']
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}

shadowJar {
    relocate 'com.zaxxer.hikari', 'com.maldahleh.db'
    archiveFileName = "StockMarket.jar"
}

tasks.build.dependsOn tasks.shadowJar